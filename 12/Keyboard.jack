// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var int keyCode;
        let keyCode = Memory.peek(24576);
        return keyCode;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key, c;

        let key = keyPressed();
        while(key = 0) {
            let key = keyPressed();
        }
        let c = key;
        let key = keyPressed();
        while(~(key = 0)) {
            let key = keyPressed();
        }
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s, s2;
        var char c;
        var int sSize;

        do Output.printString(message);

        let sSize = 10;
        let s = String.new(sSize);
        while(true){
            let c = Keyboard.readChar();
            if(c = String.newLine()) {
                do Output.println();
                return s;
            }
            if(c = String.backSpace()) {
                do s.eraseLastChar();
            } else {
                if(s.length() = sSize) {
                    let sSize = sSize + 10;
                    let s2 = s;
                    let s = String.new(sSize);
                    do String.copy(s2, s);
                }
                do s.appendChar(c);
            }
        }
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;

        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}

